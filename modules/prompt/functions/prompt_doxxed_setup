#
# A two-line, theme derived from Paradox theme
#
# This theme requires a patched Powerline font, get them from
# https://github.com/Lokaltog/powerline-fonts or https://github.com/ryanoasis/nerd-fonts
#
# Credits (for original Paradox theme):
#   Isaac Wolkerstorfer <i@agnoster.net>
#   Jeff Sandberg <paradox460@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Patrick Bos <egpbos@gmail.com>
#

# Load dependencies.
pmodload 'helper'

# Define variables.
_prompt_doxxed_current_bg='NONE'
_prompt_doxxed_segment_separator=''
_prompt_doxxed_segment_separator_alt=''
_prompt_doxxed_start_time=$SECONDS

function prompt_doxxed_start_segment {
  local bg fg
  [[ -n "$1" ]] && bg="%K{$1}" || bg="%k"
  [[ -n "$2" ]] && fg="%F{$2}" || fg="%f"
  if [[ "$_prompt_doxxed_current_bg" != 'NONE' && "$1" != "$_prompt_doxxed_current_bg" ]]; then
    print -n " $bg%F{$_prompt_doxxed_current_bg}$_prompt_doxxed_segment_separator$fg "
  else
    print -n "$bg$fg "
  fi
  _prompt_doxxed_current_bg="$1"
  [[ -n "$3" ]] && print -n "$3"
}

function prompt_doxxed_end_segment {
  if [[ -n "$_prompt_doxxed_current_bg" ]]; then
    print -n " %k%F{$_prompt_doxxed_current_bg}$_prompt_doxxed_segment_separator"
  else
    print -n "%k"
  fi
  print -n "%f"
  _prompt_doxxed_current_bg=''
}

function prompt_doxxed_build_prompt {
  prompt_doxxed_start_segment 235 yellow '%(?::%F{red}✘ )%(!:%F{yellow}⚡ :)%(1j:%F{cyan}⚙ :)%F{yellow}%n@%m%f'
  prompt_doxxed_start_segment blue black '$_prompt_doxxed_pwd'

  if [[ -n "$git_info" ]]; then
    prompt_doxxed_start_segment green black '${(e)git_info[ref]}${(e)git_info[status]}'
  fi

  if [[ -n "$python_info" ]]; then
    prompt_doxxed_start_segment white black '${(e)python_info[virtualenv]}'
  fi

  prompt_doxxed_end_segment
}

prompt_doxxed_print_elapsed_time() {
  local end_time=$(( SECONDS - _prompt_doxxed_start_time ))
  local hours minutes seconds remainder

  if (( end_time >= 3600 )); then
    hours=$(( end_time / 3600 ))
    remainder=$(( end_time % 3600 ))
    minutes=$(( remainder / 60 ))
    seconds=$(( remainder % 60 ))
    print -P "%B%F{red}>>> elapsed time ${hours}h${minutes}m${seconds}s%b"
  elif (( end_time >= 60 )); then
    minutes=$(( end_time / 60 ))
    seconds=$(( end_time % 60 ))
    print -P "%B%F{yellow}>>> elapsed time ${minutes}m${seconds}s%b"
  elif (( end_time > 10 )); then
    print -P "%B%F{green}>>> elapsed time ${end_time}s%b"
  fi
}

function prompt_doxxed_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  _prompt_doxxed_pwd=$(prompt-pwd)

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get Python environment information.
  if (( $+functions[python-info] )); then
    python-info
  fi

  # Calculate and print the elapsed time.
  prompt_doxxed_print_elapsed_time
}

function prompt_doxxed_preexec {
  _prompt_doxxed_start_time="$SECONDS"
}

function prompt_doxxed_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook preexec prompt_doxxed_preexec
  add-zsh-hook precmd prompt_doxxed_precmd

  # Tell prezto we can manage this prompt
  zstyle ':prezto:module:prompt' managed 'yes'

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format '%B%F{blue}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format '%F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%B%F{red}❮%f%b' 
 
  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ' ⁝ %s'
  zstyle ':prezto:module:git:info:added' format ' ✚'
  zstyle ':prezto:module:git:info:ahead' format ' ⬆'
  zstyle ':prezto:module:git:info:behind' format ' ⬇'
  zstyle ':prezto:module:git:info:branch' format ' %b'
  zstyle ':prezto:module:git:info:commit' format '➦ %.7c'
  zstyle ':prezto:module:git:info:deleted' format ' ✖'
  zstyle ':prezto:module:git:info:dirty' format ' ⁝'
  zstyle ':prezto:module:git:info:modified' format ' ✱'
  zstyle ':prezto:module:git:info:position' format '%p'
  zstyle ':prezto:module:git:info:renamed' format ' ➙'
  zstyle ':prezto:module:git:info:stashed' format ' S'
  zstyle ':prezto:module:git:info:unmerged' format ' ═'
  zstyle ':prezto:module:git:info:untracked' format ' ?'
  zstyle ':prezto:module:git:info:keys' format \
    'ref' '$(coalesce "%b" "%p" "%c")' \
    'status' '%s%D%A%B%S%a%d%m%r%U%u'

  # %v - virtualenv name.
  zstyle ':prezto:module:python:info:virtualenv' format 'virtualenv:%v'

  # Define prompts.
  local _lineup=$'\e[1A'
  local _linedown=$'\e[1B'
  PROMPT='
${(e)$(prompt_doxxed_build_prompt)}
 ${editor_info[keymap]} '
  RPROMPT=%{${_lineup}%}'%F{243}$_prompt_doxxed_segment_separator_alt%K{243}%F{black} %D{%H:%M:%S}%f '%{${_linedown}%}
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_doxxed_setup "$@"
# vim: ft=zsh
